{
  parserClass="com.github.dragonhatcher.natexlangplugin.language.parser.NatexParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Natex"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.dragonhatcher.natexlangplugin.language.psi"
  psiImplPackage="com.github.dragonhatcher.natexlangplugin.language.psi.impl"

  elementTypeHolderClass="com.github.dragonhatcher.natexlangplugin.language.psi.NatexTypes"
  elementTypeClass="com.github.dragonhatcher.natexlangplugin.language.psi.NatexElementType"
  tokenTypeClass="com.github.dragonhatcher.natexlangplugin.language.psi.NatexTokenType"

  psiImplUtilClass="com.github.dragonhatcher.natexlangplugin.language.psi.impl.NatexPsiImplUtil"
}

natexFile ::= obj

obj ::= L_CURLY kv ("," kv)* ","? R_CURLY

kv ::=  state | score | speaker | term_kv

private state ::= QUOTE "state" QUOTE ":" QUOTE state_name QUOTE {pin=2}
private score ::= QUOTE "score" QUOTE ":" {pin=2}
private speaker ::= QUOTE "speaker" QUOTE ":" QUOTE "system" QUOTE {pin=2}
private term_kv ::= QUOTE multi_term QUOTE ":" (obj | QUOTE state_name QUOTE) {pin=2}

state_name ::= SYMBOL {methods=[getState]}

multi_term ::= term (","? (QUOTE QUOTE)? term)*
term ::= optional | kleene_star | kleene_plus | non_kleene_term
private non_kleene_term ::= sequence | conjunction | disjunction | negation | REGEX | reference | assignment | macro | LITERAL | SYMBOL | STATE | KEYWORD

sequence ::= L_BRACKET "!"? multi_term R_BRACKET {pin=1}
conjunction ::= L_ARROW multi_term R_ARROW {pin=1}
disjunction ::= L_CURLY multi_term R_CURLY {pin=1}
optional ::= non_kleene_term "?"
kleene_star ::= non_kleene_term "*"
kleene_plus ::= non_kleene_term "+"
negation ::= "-" term
reference ::= "$" SYMBOL
assignment ::= "$" SYMBOL "=" term
macro ::= MACRO_NAME ( L_PAREN macro_arg? (","? " "? macro_arg)* R_PAREN )? {pin=1}
macro_arg ::= MACRO_ARG_STRING | MACRO_LITERAL | macro
x ::= PUNCUATION

private expr_recover ::= !("," | "}" | "{")